#ifndef _LIGHT_INPUT_CMN_
#define _LIGHT_INPUT_CMN_

#if defined(COMPUTE_SHADER)
#   define ACCESS
#else
#   define ACCESS readonly
#endif

const int LIGHT_DIRECTIONAL = 0;
const int LIGHT_OMNIDIRECTIONAL = 1;
const int LIGHT_SPOT = 2;

struct Light {
    /// rgb = colour
    /// w = reserved
    vec4   _colour;
    /// xyz = light positon (or direction for Directional lights)
    /// w = range
    vec4   _positionWV; 
    /// xyz = spot direction
    /// w = spot angle   
    vec4   _directionWV;
    /// x = light type: 0.0 - directional, 1.0  - point, 2.0 - spot
    /// y = casts shadows
    /// z = shadow block index
    /// w = if directional light : csm split count else : reserved
    ivec4  _options;
};

layout(binding = BUFFER_LIGHT_NORMAL, std430) coherent ACCESS buffer dvd_LightBlock
{
    Light dvd_LightSource[MAX_POSSIBLE_LIGHTS];
};

layout(binding = BUFFER_LIGHT_INDICES, std430) coherent ACCESS buffer perTileLightIndexBuffer
{
    uint perTileLightIndices[];
};

#if defined(FRAG_SHADER)
struct Shadow {
    uvec4 _arrayOffset;
    mat4  _lightVP[MAX_SPLITS_PER_LIGHT];
    vec4  _lightPosition[MAX_SPLITS_PER_LIGHT];
    vec4  _floatValues[MAX_SPLITS_PER_LIGHT];
};

layout(binding = BUFFER_LIGHT_SHADOW, std430) coherent ACCESS buffer dvd_ShadowBlock
{
    Shadow dvd_ShadowSource[MAX_SHADOW_CASTING_LIGHTS];
};

#endif 

#define windowWidth int(dvd_ViewPort.z)
#define windowHeight int(dvd_ViewPort.w)

// calculate the number of tiles in the horizontal direction
uint GetNumTilesX()
{
    return uint((windowWidth + FORWARD_PLUS_TILE_RES - 1) / float(FORWARD_PLUS_TILE_RES));
}

// calculate the number of tiles in the vertical direction
uint GetNumTilesY()
{
    return uint((windowHeight + FORWARD_PLUS_TILE_RES - 1) / float(FORWARD_PLUS_TILE_RES));
}

uint GetTileIndex(in vec2 ScreenPos)
{
    float fTileRes = float(FORWARD_PLUS_TILE_RES);
    return uint(floor(ScreenPos.x / fTileRes) + floor(ScreenPos.y / fTileRes) * GetNumTilesX());
}

#endif //_LIGHT_INPUT_CMN_